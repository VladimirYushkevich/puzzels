buildscript {
    repositories {
        mavenCentral()
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    sourceCompatibility = 11
    targetCompatibility = 11

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        jcenter()
    }

    test {
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}